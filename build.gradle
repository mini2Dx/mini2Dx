import org.apache.tools.ant.taskdefs.condition.Os
import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
    repositories {
    	mavenLocal()
        mavenCentral()
        maven { url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/' }
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
		classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.mobidevelop.robovm:robovm-gradle-plugin:2.2.0'
        classpath 'gradle.plugin.me.champeau.gradle:jmh-gradle-plugin:0.3.0'
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
		classpath 'com.badlogicgames.gdx:gdx-tools:1.9.2'
    }
}

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		packageGroup = 'org.mini2Dx'
		username = ossrhUser
		password = ossrhPassword
	}
}

ext {
	nativesLoaderVersion="1.0.0"
	gdxVersion="1.9.4"
	roboVMVersion="2.2.0"
	androidVersion="4.4"
	beanutilsVersion="1.8.3"
	langVersion="3.1"
	reflectionsVersion="0.9.10"
	artemisVersion="1.4.0"
	androidBeansVersion="1.0.0"
	
	junitVersion="4.8.1"
	jmockVersion="2.5.1"
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    
    tasks.eclipse.doLast {
    	delete ".project"
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url "https://oss.sonatype.org/content/repositories/releases/" }
	}
}

def docProjects() {
	subprojects.findAll { !it.name.contains('uat') }
}

configure(docProjects()) {
	apply plugin: "java"
	apply plugin: "signing"
	apply plugin: "maven"
	
	group = 'org.mini2Dx'
	version = '1.4.1-SNAPSHOT'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	configurations {
		testOutput.extendsFrom (testCompile)
	}
	
	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
	
	task testJar(type: Jar, dependsOn: testClasses) {
		from sourceSets.test.output
		include '**/serialization/**/*.class','**/di/**/*.class'
		classifier = 'test'
	}

	artifacts {
		archives javadocJar, sourcesJar, testJar
		testOutput testJar
	}
	
	// Maven Central publication
	if(project.hasProperty('ossrhUser')) {
		signing {
			sign configurations.archives
		}
		
		uploadArchives {
			repositories {
				mavenDeployer {
					// POM signature
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
					// Target repository
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						authentication(userName: ossrhUser, password: ossrhPassword)
					}
					pom.project {
						name project.name
						description project.description
						packaging 'jar'
						url 'https://github.com/mini2Dx/mini2Dx'
	
						scm {
							connection 'scm:git:https://github.com/mini2Dx/mini2Dx.git'
							developerConnection 'scm:git:git@github.com:mini2Dx/mini2Dx.git'
							url 'https://github.com/mini2Dx/mini2Dx.git'
						}
	
						licenses {
							license {
								name 'The BSD 3-Clause License'
								url 'https://opensource.org/licenses/BSD-3-Clause'
								distribution 'repo'
							}
						}
	
						developers {
							developer {
								id = 'tomcashman'
								name = 'Thomas Cashman'
								email = 'thomascashman404@gmail.com'
							}
						}
					}
				}
			}
		}
	}
}

project(":" + rootProject.name + "-core") {
	description = "mini2Dx core library"
    
    apply plugin: "me.champeau.gradle.jmh"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
        
		compile "org.mini2Dx:natives-loader:$nativesLoaderVersion"
        compile "org.apache.commons:commons-lang3:$langVersion"
        
		testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
    }
	
	jmh {
		timeUnit = 'ns'
		threads = Runtime.getRuntime().availableProcessors()
		warmupIterations = 10
		iterations = 10
		fork = Runtime.getRuntime().availableProcessors()
		jvmArgsPrepend = '-Xmx1024m'
	}
}

project(":" + rootProject.name + "-desktop") {
	description = "mini2Dx desktop runtime"
	
    dependencies {
    	compile project(":" + rootProject.name + "-core")
    	
    	compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"
    	
		compile "commons-beanutils:commons-beanutils:$beanutilsVersion"
    	compile "org.reflections:reflections:$reflectionsVersion"
    	
    	testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
		testCompile project(path: ":" + rootProject.name + "-core", configuration: 'testOutput')
    }
}

project(":" + rootProject.name + "-headless") {
	description = "mini2Dx headless runtime"
	
    dependencies {
    	compile project(":" + rootProject.name + "-desktop")
    	
    	compile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    	
    	testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
		testCompile project(path: ":" + rootProject.name + "-core", configuration: 'testOutput')
    }
}

project(":" + rootProject.name + "-tiled") {
	description = "mini2Dx Tiled support library"
	
    dependencies {
		compile project(":" + rootProject.name + "-core")
		
		testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
    }
}

project(":" + rootProject.name + "-artemis-odb") {
	description = "mini2Dx artemis-odb support library"
	
    dependencies {
		compile project(":" + rootProject.name + "-core")
		compile "net.onedaybeard.artemis:artemis-odb:$artemisVersion"
		
		testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
    }
}

project(":" + rootProject.name + "-ui") {
	description = "mini2Dx UI library"
	
	apply plugin: "me.champeau.gradle.jmh"
	
	dependencies {
		compile project(":" + rootProject.name + "-core")
		
		testCompile "junit:junit:$junitVersion"
		testCompile "org.jmock:jmock-junit4:$jmockVersion"
		testCompile "org.jmock:jmock-legacy:$jmockVersion"
		testCompile project(":" + rootProject.name + "-desktop")
		
		jmh project(":" + rootProject.name + "-headless")
	}
	
	task packDefaultUiTheme << {
		TexturePacker.process("$projectDir/default-mdx-theme", "$projectDir/src/main/resources", "default-mdx-theme")
	}
	
	jmh {
		timeUnit = 'ns'
		threads = Runtime.getRuntime().availableProcessors()
		warmupIterations = 10
		iterations = 10
		fork = Runtime.getRuntime().availableProcessors()
		jvmArgsPrepend = '-Xmx1024m'
	}
}

project(":" + rootProject.name + "-uats") {
	description = "mini2Dx UATs library"
	
    apply plugin: "java"
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7

    dependencies {
    	compile project(":" + rootProject.name + "-tiled")
		compile project(":" + rootProject.name + "-ui")
    }
	
	task docs(type: Javadoc) {
		title = 'mini2Dx'
		source docProjects().collect {p -> p.sourceSets.main.allJava }
		classpath = files(docProjects().collect {p -> p.sourceSets.main.compileClasspath})
		destinationDir = new File(project.parent.projectDir, 'docs')
	}
	docs.dependsOn docProjects().build
}

project(":" + rootProject.name + "-uats-desktop") {
	description = "mini2Dx desktop UATs launcher"
	
    apply plugin: "java"
    
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(":" + rootProject.name + "-uats")
        compile project(":" + rootProject.name + "-desktop")
		compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

if (hasAndroidSdk()) {
	project(":" + rootProject.name + "-android") {
		description = "mini2Dx Android runtime"
		
		dependencies {
			compile project(":" + rootProject.name + "-core")
			compile files(fileTree(dir: 'libs', includes: ['*.jar']))
			compile "org.mini2Dx:android-beans:$androidBeansVersion"
			
			compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
			compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
			compile "com.badlogicgames.gdx:gdx-controllers-android:$gdxVersion"
		}
	}
	
	project(":" + rootProject.name + "-uats-android") {
		description = "mini2Dx Android UATs launcher"
		
		apply plugin: "com.android.application"
		
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
		
		ext {
			appName = 'mini2Dx-uats'
		}
		
		configurations { 
			natives
		}
		
		dependencies {
			compile project(":" + rootProject.name + "-uats")
			compile project(":" + rootProject.name + "-android")
			
			natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
       		natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
       		natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
       		natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
			
			androidTestCompile (project(path: ":" + rootProject.name + "-core", configuration: 'testOutput')) {
				exclude group: 'junit'
				exclude group: 'org.hamcrest'
			}
			androidTestCompile ("org.jmock:jmock-junit4:$jmockVersion") {
				exclude group: 'junit'
				exclude group: 'org.hamcrest'
			}
			androidTestCompile ("org.jmock:jmock-legacy:$jmockVersion") {
				exclude group: 'junit'
				exclude group: 'org.hamcrest'
			}
			androidTestCompile 'com.android.support.test:runner:0.4'
			androidTestCompile 'com.android.support.test:rules:0.4'
		}
	}
}

if (Os.isFamily(Os.FAMILY_MAC)) {
	project(":" + rootProject.name + "-ios") {
		description = "mini2Dx iOS runtime"
	
		dependencies {
			compile project(":" + rootProject.name + "-core")
			
			compile "com.mobidevelop.robovm:robovm-rt:$roboVMVersion"
        	compile "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion"
			compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
			compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
			compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-ios"
			
			compile "org.reflections:reflections:$reflectionsVersion"
			compile "commons-beanutils:commons-beanutils:$beanutilsVersion"
			
			testCompile "junit:junit:$junitVersion"
			testCompile "org.jmock:jmock-junit4:$jmockVersion"
			testCompile "org.jmock:jmock-legacy:$jmockVersion"
		}
	}
	
	project(":" + rootProject.name + "-uats-ios") {
		description = "mini2Dx UATs iOS launcher"
		
		apply plugin: "java"
		apply plugin: "robovm"
		
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
		
		configurations { natives }
		
		eclipse.project {
			natures 'org.robovm.eclipse.RoboVMNature'
		}
		
		dependencies {
			compile project(":" + rootProject.name + "-uats")
			compile project(":" + rootProject.name + "-ios")
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

boolean hasAndroidSdk() {
	try {
		def rootDir = "$rootDir"
		def localProperties = new File(rootDir, "local.properties")
		if (localProperties.exists()) {
			Properties properties = new Properties()
			localProperties.withInputStream { instr ->
				properties.load(instr)
			}
			def androidHome = properties.getProperty('sdk.dir')
			if(androidHome != null && androidHome.length() > 0) {
				println androidHome
				return true;
			}
		}
	} catch (all) {
		println all
	}
	try {
		def androidHome = "$System.env.ANDROID_HOME"
		if(androidHome != null && androidHome.length() > 0 && !androidHome.contains('null')) {
			println androidHome
			return true;
		}
	} catch (all) {
		println all
	}
	
	try {
		def androidExecPath = new ByteArrayOutputStream()
		if (Os.isFamily(Os.FAMILY_WINDOWS)) {
			return false
		} else {
			exec {
				commandLine 'which', 'android'
				standardOutput = androidExecPath
			}
		}
		return androidExecPath.toString() != null && androidExecPath.toString().trim().length() > 0;
	} catch (all) {
		println all
	}
	return false
}
