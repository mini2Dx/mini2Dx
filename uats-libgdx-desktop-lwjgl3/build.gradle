sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "org.mini2Dx.uats.desktop.Lwjgl3UATApplication"
project.ext.assetsDir = new File("$projectDir/../uats-libgdx-desktop/assets");

sourceSets.main.resources.srcDirs += project.ext.assetsDir.getAbsolutePath()

task launchDesktop3(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        // Required to run lwjgl java apps on Mac OSX
        jvmArgs = ["-XstartOnFirstThread"]
    }
}

eclipse {
    project {
        name = "mini2Dx-uats-desktop"
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/uats-android/assets'
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
  doLast {
    def classpath = new XmlParser().parse(file(".classpath"))
    new Node(classpath, "classpathentry", [ kind: 'src', path: 'assets' ]);
    def writer = new FileWriter(file(".classpath"))
    def printer = new XmlNodePrinter(new PrintWriter(writer))
    printer.setPreserveWhitespace(true)
    printer.print(classpath)
  }
}